plugins {
    id "de.undercouch.download" version "4.0.2"
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

/*
 * MIT License
 *
 * Copyright (c) 2018- Mosaic Networks
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

//##################################################################################################
// babble core configuration

def babbleCoreVersion = "0.7.0"
def repository = "https://github.com/mosaicnetworks/babble/releases/download"
def protocol = "https"

// def babbleCoreVersion = "develop_011243e4f3e102b75ae15cb5692314a7cecdbd87"
// def repository = (System.getenv('GOPATH') == null ? System.properties['user.home'] + "/go" : System.getenv('GOPATH'))  + "/src/github.com/mosaicnetworks/babble/build/distmobile"
// def protocol = "file"

//##################################################################################################

def babbleDependency = "libs/babble-core/$babbleCoreVersion/classes.jar"

task getBabbleCore {

    def versionDirectory = "$projectDir/libs/babble-core/$babbleCoreVersion"
    def versionDirectoryFile = new File(versionDirectory)
    def releaseZipUrl = "$repository/v${babbleCoreVersion}/babble_${babbleCoreVersion}_android_library.zip"
    def fileZipLoc = "$repository/babble_${babbleCoreVersion}_android_library.zip"
    def downloadedFileName = "babble_${babbleCoreVersion}_android_library.zip"
    def downloadFullPath = "$versionDirectory/tmp/$downloadedFileName"
    def downloadFullPathFile = new File(downloadFullPath)
    def aarFullPath = "$versionDirectory/tmp/babble_${babbleCoreVersion}_mobile.aar"
    def aarFullPathFile = new File(aarFullPath)

    if (versionDirectoryFile.exists()) {
        print("Found babble-core version $babbleCoreVersion\n")
    } else {
        print("Could not find babble-core version $babbleCoreVersion in $versionDirectory\n")
        versionDirectoryFile.mkdirs()

        if (protocol=="file") {

            print("Attempting to copy from $fileZipLoc\n")
            copy {
                from fileZipLoc
                into "$versionDirectory/tmp"
            }


        } else if (protocol=="https") {

            print("Attempting to download from $releaseZipUrl\n")

            download {
                src releaseZipUrl
                dest downloadFullPath
            }
        }

        copy {
            from zipTree(downloadFullPathFile)
            into "$versionDirectory/tmp"
        }

        copy {
            from zipTree(aarFullPathFile)
            into "$versionDirectory/tmp/aar-unzip"
        }

        copy {
            from "$versionDirectory/tmp/aar-unzip/classes.jar"
            into versionDirectory
        }

        copy {
            from "$versionDirectory/tmp/aar-unzip/jni"
            into "$versionDirectory/jni"
        }

        copy {
            from "$versionDirectory/tmp/git.version"
            into "$versionDirectory"
        }


        delete {
            delete "$versionDirectory/tmp"
        }

    }
}


android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21       // nsdServiceInfo.getAttributes requires API 21
        targetSdkVersion 29
        versionCode 11
        versionName "0.4.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["libs/babble-core/$babbleCoreVersion/jni"]
        }
    }

}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.nanohttpd:nanohttpd:2.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    implementation 'com.android.volley:volley:1.1.1'
    implementation files(babbleDependency)
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.google.guava:guava:28.2-android'
    implementation 'com.google.android.material:material:1.2.0-alpha04'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
}

artifacts {
    archives sourcesJar
}

version '0.4.0'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('user')
    key = properties.getProperty('apiKey')
    publications = ['MyPublication']
    configurations = ['archives']
    publish = true //[Default: false] Whether version should be auto published after an upload
    override = false //[Default: false] Whether to override version artifacts already published

    pkg {
        repo = 'maven'
        userOrg = 'mosaicnetworks'
        name = 'io.mosaicnetworks:babble'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mosaicnetworks/babble-android.git'
        version {
            name = this.version
            released  = new Date()
            vcsTag = this.version
        }
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        MyPublication(MavenPublication) {

            artifact sourcesJar
            artifact javadocJar

            pom {
                packaging = "aar"
            }

            pom.withXml {

                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }


        }
    }
}







